#define LAST_STATE_PLANET "on_planet"
#define LAST_STATE_SPACE "in_space"

/datum/quirk/spacer_born
	name = "Spacer"
	desc = "(Космолит) - Вы родились в космосе и никогда не знали комфорта планетарной гравитации. Ваше тело адаптировалось к этому. \
		Вы чувствуете себя более комфортно в невесомости и искусственной гравитации и более устойчивы к воздействию космоса, \
		но длительное пребывание на поверхности планеты вызовет у вас недомогание."
	gain_text = span_notice("Вы чувствуете себя как дома в космосе.")
	lose_text = span_danger("Вы чувствуете тоску по дому.")
	icon = FA_ICON_USER_ASTRONAUT
	value = 5
	quirk_flags = QUIRK_HUMAN_ONLY|QUIRK_CHANGES_APPEARANCE
	medical_record_text = "Пациент хорошо адаптирован к внеземным условиям."
	mail_goodies = list(
		/obj/item/storage/pill_bottle/ondansetron,
		/obj/item/reagent_containers/applicator/pill/gravitum,
	)
	/// Насколько увеличивается рост космолитов
	var/modded_height = HUMAN_HEIGHT_TALLER
	/// Сколько времени на планете до появления негативных эффектов
	var/planet_period = 3 MINUTES
	/// TimerID для времени, проведенного на планете
	VAR_FINAL/planetside_timer
	/// Сколько времени в космосе до появления положительных эффектов
	var/recover_period = 1 MINUTES
	/// TimerID для времени, проведенного в космосе
	VAR_FINAL/recovering_timer
	/// Определяет последнее состояние, в котором мы находились ([LAST_STATE_PLANET] или [LAST_STATE_SPACE])
	VAR_FINAL/last_state

/datum/quirk/spacer_born/add(client/client_source)
	if(isdummy(quirk_holder))
		return

	// Используем Z changed, потому что нам не нужно срочно проверять статус планеты при каждом движении по turf.
	// Если вы прибыли на "планету", вся Z-координата будет "планетой".
	// Не имеет смысла пройти 3 фута и внезапно получить/потерять гравитационную болезнь.
	// Если я ошибаюсь, можно заменить это на Moved.
	RegisterSignal(quirk_holder, COMSIG_MOVABLE_Z_CHANGED, PROC_REF(spacer_moved))

	// Да, предполагается, что для планетарных карт вы начинаете с гравитационной болезни.
	check_z(quirk_holder, skip_timers = TRUE)

	// Дрейфуйте немного быстрее в невесомости
	quirk_holder.inertia_move_multiplier *= 0.8

	var/mob/living/carbon/human/human_quirker = quirk_holder
	human_quirker.set_mob_height(modded_height)
	human_quirker.physiology.pressure_mod *= 0.8
	human_quirker.physiology.cold_mod *= 0.8

/datum/quirk/spacer_born/post_add()
	var/on_a_planet = SSmapping.is_planetary()
	var/planet_job = istype(quirk_holder.mind?.assigned_role, /datum/job/shaft_miner)
	if(!on_a_planet && !planet_job)
		return
	var/datum/bank_account/spacer_account = quirk_holder.get_bank_account()
	if(!isnull(spacer_account))
		spacer_account.payday_modifier *= 1.25
		to_chat(quirk_holder, span_info("Учитывая ваше происхождение как Космолита, \
			вам начисляется 25% надбавки за риск из-за вашего [on_a_planet ? "станционного" : "профессионального"] назначения."))

	// Снабжаем их пластырями для помощи на новом месте работы
	var/obj/item/storage/pill_bottle/ondansetron/disgust_killers = new()
	disgust_killers.desc += " Лучше принять один при путешествии на поверхность планеты."
	if(quirk_holder.equip_to_storage(disgust_killers, ITEM_SLOT_BACK, indirect_action = TRUE, del_on_fail = TRUE))
		to_chat(quirk_holder, span_info("Вам [isnull(spacer_account) ? " " : " также "]выдали противотошнотные пластыри для помощи в адаптации к планетарной гравитации."))

/datum/quirk/spacer_born/remove()
	UnregisterSignal(quirk_holder, COMSIG_MOVABLE_Z_CHANGED)

	if(QDELING(quirk_holder))
		return

	quirk_holder.inertia_move_multiplier /= 0.8
	quirk_holder.clear_mood_event("spacer")
	quirk_holder.remove_movespeed_modifier(/datum/movespeed_modifier/spacer)
	quirk_holder.remove_status_effect(/datum/status_effect/spacer)

	var/mob/living/carbon/human/human_quirker = quirk_holder
	human_quirker.set_mob_height(HUMAN_HEIGHT_MEDIUM)
	human_quirker.physiology.pressure_mod /= 0.8
	human_quirker.physiology.cold_mod /= 0.8

/// Проверяет при изменении Z, должны ли мы запустить или остановить таймеры
/datum/quirk/spacer_born/proc/spacer_moved(mob/living/source, turf/old_turf, turf/new_turf, same_z_layer)
	SIGNAL_HANDLER

	check_z(source)

/**
 * Используется для проверки, должны ли мы запустить или остановить таймеры на основе местоположения владельца особенности.
 *
 * * afflicted - моб, прибывающий / тот же, что и владелец особенности
 * * skip_timers - если TRUE, это выполняется мгновенно / не должно иметь обратной связи (например, при инициализации)
 */
/datum/quirk/spacer_born/proc/check_z(mob/living/spacer, skip_timers = FALSE)
	if(is_on_a_planet(spacer))
		on_planet(spacer, skip_timers)
	else
		in_space(spacer, skip_timers)

// Переход на планету

/**
 * Выполняется при прибытии на планету.
 *
 * * afflicted - моб, прибывающий / тот же, что и владелец особенности
 * * skip_timers - если TRUE, это выполняется мгновенно / не должно иметь обратной связи (например, при инициализации)
 */
/datum/quirk/spacer_born/proc/on_planet(mob/living/afflicted, skip_timers = FALSE)
	if(planetside_timer || last_state == LAST_STATE_PLANET)
		return
	if(recovering_timer)
		deltimer(recovering_timer)
		recovering_timer = null

	last_state = LAST_STATE_PLANET

	if(skip_timers)
		on_planet_for_too_long(afflicted, TRUE)
		return

	// Недавние физические нагрузки позволяют нам дольше выдерживать тяжелые нагрузки
	var/exercise_bonus = afflicted.has_status_effect(/datum/status_effect/exercised) ? 2 : 1
	planetside_timer = addtimer(CALLBACK(src, PROC_REF(on_planet_for_too_long), afflicted), planet_period * exercise_bonus, TIMER_STOPPABLE)
	afflicted.add_mood_event("spacer", /datum/mood_event/spacer/on_planet)
	afflicted.add_movespeed_modifier(/datum/movespeed_modifier/spacer/on_planet)
	afflicted.remove_status_effect(/datum/status_effect/spacer) // убирает эффект благополучия.
	to_chat(afflicted, span_danger("Вам немного нехорошо под местной гравитацией."))

/**
 * Выполняется после слишком долгого пребывания на планете.
 *
 * * afflicted - моб, прибывающий / тот же, что и владелец особенности
 * * skip_timers - если TRUE, это выполняется мгновенно / не должно иметь обратной связи (например, при инициализации)
 */
/datum/quirk/spacer_born/proc/on_planet_for_too_long(mob/living/afflicted, skip_timers = FALSE)
	if(QDELETED(src) || QDELETED(afflicted))
		return

	// Слегка уменьшенные эффекты на планетарных картах для большей переносимости
	var/nerfed_effects_because_planetary = SSmapping.is_planetary()
	var/moodlet_picked = nerfed_effects_because_planetary ? /datum/mood_event/spacer/on_planet/nerfed : /datum/mood_event/spacer/on_planet/too_long
	var/movespeed_mod_picked = nerfed_effects_because_planetary ? /datum/movespeed_modifier/spacer/on_planet/nerfed : /datum/movespeed_modifier/spacer/on_planet/too_long

	planetside_timer = null
	afflicted.apply_status_effect(/datum/status_effect/spacer/gravity_sickness)
	afflicted.add_mood_event("spacer", moodlet_picked)
	afflicted.add_movespeed_modifier(movespeed_mod_picked)

	if(!skip_timers)
		to_chat(afflicted, span_danger("Вы пробыли здесь слишком долго. Гравитация действительно начинает действовать вам на нервы."))

// Возвращение в космос

/**
 * Выполняется при возвращении в космос / место с низкой гравитацией.
 *
 * * afflicted - моб, прибывающий / тот же, что и владелец особенности
 * * skip_timers - если TRUE, это выполняется мгновенно / не должно иметь обратной связи (например, при инициализации)
 */
/datum/quirk/spacer_born/proc/in_space(mob/living/afflicted, skip_timers = FALSE)
	if(recovering_timer || last_state == LAST_STATE_SPACE)
		return
	if(planetside_timer)
		deltimer(planetside_timer)
		planetside_timer = null

	last_state = LAST_STATE_SPACE

	if(skip_timers)
		comfortably_in_space(afflicted, TRUE)
		return

	recovering_timer = addtimer(CALLBACK(src, PROC_REF(comfortably_in_space), afflicted), recover_period, TIMER_STOPPABLE)
	afflicted.remove_status_effect(/datum/status_effect/spacer)
	afflicted.clear_mood_event("spacer")
	// Пока не убираем модификатор движения, он сохраняется до полного восстановления
	to_chat(afflicted, span_green("Вам становится лучше теперь, когда вы вернулись в космос."))

/**
 * Выполняется при достаточно долгом пребывании в космосе.
 *
 * * afflicted - моб, прибывающий / тот же, что и владелец особенности
 * * skip_timers - если TRUE, это выполняется мгновенно / не должно иметь обратной связи (например, при инициализации)
 */
/datum/quirk/spacer_born/proc/comfortably_in_space(mob/living/afflicted, skip_timers = FALSE)
	if(QDELETED(src) || QDELETED(afflicted))
		return

	recovering_timer = null
	afflicted.apply_status_effect(/datum/status_effect/spacer/gravity_wellness)
	afflicted.add_mood_event("spacer", /datum/mood_event/spacer/in_space)
	afflicted.add_movespeed_modifier(/datum/movespeed_modifier/spacer/in_space)
	if(!skip_timers)
		to_chat(afflicted, span_green("Вы чувствуете себя лучше."))

#undef LAST_STATE_PLANET
#undef LAST_STATE_SPACE
